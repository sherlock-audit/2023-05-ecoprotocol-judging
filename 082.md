ravikiran.web3

medium

# inflationMultiplier can be manipulated to benefit from deposit and withdrawls across chains.

## Summary
Inflation multiplier not in sync between two chain opens up vulnerability. lets say, Alice uses a flash loan to deposit and withdrawn on L1 effecting the Inflation Multipler as inflationMultiplier is update in the state variable in the same call.

 inflationMultiplier = IECO(l1Eco).getPastLinearInflation(
            block.number
        );

But, the sendCrossDomainMessage fired in L1 might have to wait a block or two depending on gas to get commited into the L2 blockchain.

The difference in the inflation Multiplier can directly effect the amounts deposited and withdrawn during these times of difference.
The manipulator can use this window to grab a position in L2 and withdraw later with updated inflationMultiplier resulting in larger number of tokens.

## Vulnerability Detail
difference in the inflation Multiplier across L1 and L2 creates opporutines to manipuate the tokens minted and burn in the caller's favour.

## Impact
Get more tokens using the window of difference in inflation between two blockchain

## Code Snippet

**Deposit cycle**
https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L190-L206

```solidity
function depositERC20(
        address _l1Token,
        address _l2Token,
        uint256 _amount,
        uint32 _l2Gas,
        bytes calldata _data
    ) external virtual onlyEOA isL1EcoToken(_l1Token) isL2EcoToken(_l2Token) {
        _initiateERC20Deposit(
            _l1Token,
            _l2Token,
            msg.sender,
            msg.sender,
            _amount,
            _l2Gas,
            _data
        );
    }

```

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L321-L361

```solidity
 function _initiateERC20Deposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _l2Gas,
        bytes calldata _data
    ) internal {
        // When a deposit is initiated on L1, the L1 Bridge transfers the funds to itself for future
        // withdrawals.

        IECO(_l1Token).transferFrom(_from, address(this), _amount);
        // gons move across the bridge, with inflation multipliers on either side to correctly scale balances
        _amount = _amount * inflationMultiplier;

        // Construct calldata for _l2Token.finalizeDeposit(_to, _amount)
        bytes memory message = abi.encodeWithSelector(
            //call parent interface IL2ERC20Bridge to get the selector
            IL2ERC20Bridge.finalizeDeposit.selector,
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );

        // Send calldata into L2
        sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);

        emit ERC20DepositInitiated(
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );
    }
```


https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L2ECOBridge.sol#L145-L164

```solidity
 function finalizeDeposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _data
    )
        external
        virtual
        onlyFromCrossDomainAccount(l1TokenBridge)
        isL1EcoToken(_l1Token)
        isL2EcoToken(_l2Token)
    {
        // When a deposit is finalized, we convert the transferred gons to ECO using the current
        // inflation multiplier, then we credit the account on L2 with the same amount of tokens.
        _amount = _amount / inflationMultiplier;
        L2ECO(_l2Token).mint(_to, _amount);
        emit DepositFinalized(_l1Token, _l2Token, _from, _to, _amount, _data);
    }
```

Withdrawl Cycle:
https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L2ECOBridge.sol#L120-L127
```solidity
function withdraw(
        address _l2Token,
        uint256 _amount,
        uint32 _l1Gas,
        bytes calldata _data
    ) external virtual isL2EcoToken(_l2Token) {
        _initiateWithdrawal(msg.sender, msg.sender, _amount, _l1Gas, _data);
    }
```

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L2ECOBridge.sol#L229C1-L263

```solidity
function _initiateWithdrawal(
        address _from,
        address _to,
        uint256 _amount,
        uint32 _l1Gas,
        bytes calldata _data
    ) internal {
        // Burn the withdrawn tokens from L2
        l2Eco.burn(msg.sender, _amount);

        // Construct calldata for l1TokenBridge.finalizeERC20Withdrawal(_to, _amount)
        _amount = _amount * inflationMultiplier;
        bytes memory message = abi.encodeWithSelector(
            //call parent interface of IL1ECOBridge to get the selector
            IL1ERC20Bridge.finalizeERC20Withdrawal.selector,
            l1Eco,
            l2Eco,
            _from,
            _to,
            _amount,
            _data
        );

        // Send message up to L1 bridge
        sendCrossDomainMessage(l1TokenBridge, _l1Gas, message);

        // Emit event to notify L2 of withdrawal
        emit WithdrawalInitiated(
            l1Eco,
            address(l2Eco),
            msg.sender,
            _to,
            _amount,
            _data
        );
    }
```

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L236-L291

```solidity
  function finalizeERC20Withdrawal(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _gonsAmount,
        bytes calldata _data
    ) external onlyFromCrossDomainAccount(l2TokenBridge) {
        uint256 _amount = _gonsAmount / inflationMultiplier;

        // equivalent to IECO(_l1Token).transfer(_to, _amount); but is revert safe
        bytes memory _ecoTransferMessage = abi.encodeWithSelector(
            IERC20.transfer.selector,
            _to,
            _amount
        );
        (bool success, bytes memory returnData) = _l1Token.call{value: 0}(
            _ecoTransferMessage
        );

        // make sure that the call to transfer didn't revert or return false
        if (success && abi.decode(returnData, (bool))) {
            // if successful, emit an event
            emit ERC20WithdrawalFinalized(
                _l1Token,
                _l2Token,
                _from,
                _to,
                _amount,
                _data
            );
        } else {
            // if the transfer fails, create a return tx
            bytes memory message = abi.encodeWithSelector(
                IL2ERC20Bridge.finalizeDeposit.selector,
                _l1Token,
                _l2Token,
                _to, // switched the _to and _from here to bounce back the deposit to the sender
                _from,
                _gonsAmount,
                _data
            );

            // Send message up to L1 bridge
            sendCrossDomainMessage(l2TokenBridge, 0, message);
            // Emit an event to signal success event listeners to expect failure
            emit WithdrawalFailed(
                _l1Token,
                _l2Token,
                _from,
                _to,
                _amount,
                _data
            );
        }
    }
```

## Tool used

Manual Review

## Recommendation
Keeping inflationMultiplier in synch between the two blockchains using crossDomainMessager will minimize the possibilities for such manipulations.
