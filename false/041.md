Bauchibred

high

# L2ECOBridge.sol: Incomplete implementation of the `finalizeDeposit()` Function Could Cost Users their Tokens

## Summary

The `finalizeDeposit()` function in the `L2ECOBridge` contract has an incomplete implementation that fails to account for scenarios where the deposit transaction fails unlike it's inherited counter part (i.e optimism's [L2StandardBridge.finaldeposit()](https://github.com/ethereum-optimism/optimism/blob/9ddfe5706f073eb0a1906216694e9b5e22ad4a8c/packages/contracts/contracts/L2/messaging/L2StandardBridge.sol#L141-L187)). This missing handling could result in inconsistencies and undesired behavior within the bridge mechanism since if the transaction ever fails there is no how users can get their tokens back.

## Vulnerability Detail

The [finalizeDeposit()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/b440f89234b806f672b9e9ad24cf70c409964db5/op-eco/contracts/bridge/L2ECOBridge.sol#L145-L164) function is responsible for finalizing a deposit from L1 to L2 by converting the transferred "gons" to ECO tokens using the current inflation multiplier and crediting the recipient's address on L2 with the corresponding amount of tokens. However, the function does not consider the possibility of a failed deposit transaction, which could result in incorrect token balances and inconsistent state between L1 and L2, and could lead to the loss of tokens of users

```solidity
    function finalizeDeposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _data
    )
        external
        virtual
        onlyFromCrossDomainAccount(l1TokenBridge)
        isL1EcoToken(_l1Token)
        isL2EcoToken(_l2Token)
    {
        // When a deposit is finalized, we convert the transferred gons to ECO using the current
        // inflation multiplier, then we credit the account on L2 with the same amount of tokens.
        _amount = _amount / inflationMultiplier;
        L2ECO(_l2Token).mint(_to, _amount);
        emit DepositFinalized(_l1Token, _l2Token, _from, _to, _amount, _data);
    }
```

Take a look at the original implementation of the [finaldeposit()](https://github.com/ethereum-optimism/optimism/blob/9ddfe5706f073eb0a1906216694e9b5e22ad4a8c/packages/contracts/contracts/L2/messaging/L2StandardBridge.sol#L141-L187) function from optimism below. As visibly seen there are different scenarios where a deposit tx could fail and the function implements a method to account for this by sending user's token back to them if this ever happens

```solidity
function finalizeDeposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _data
    ) external virtual onlyFromCrossDomainAccount(l1TokenBridge) {
        // Check the target token is compliant and
        // verify the deposited token on L1 matches the L2 deposited token representation here
        if (
            // slither-disable-next-line reentrancy-events
            ERC165Checker.supportsInterface(_l2Token, 0x1d1d8b63) &&
            _l1Token == IL2StandardERC20(_l2Token).l1Token()
        ) {
            // When a deposit is finalized, we credit the account on L2 with the same amount of
            // tokens.
            // slither-disable-next-line reentrancy-events
            IL2StandardERC20(_l2Token).mint(_to, _amount);
            // slither-disable-next-line reentrancy-events
            emit DepositFinalized(_l1Token, _l2Token, _from, _to, _amount, _data);
        } else {
            // Either the L2 token which is being deposited-into disagrees about the correct address
            // of its L1 token, or does not support the correct interface.
            // This should only happen if there is a  malicious L2 token, or if a user somehow
            // specified the wrong L2 token address to deposit into.
            // In either case, we stop the process here and construct a withdrawal
            // message so that users can get their funds out in some cases.
            // There is no way to prevent malicious token contracts altogether, but this does limit
            // user error and mitigate some forms of malicious contract behavior.
            bytes memory message = abi.encodeWithSelector(
                IL1ERC20Bridge.finalizeERC20Withdrawal.selector,
                _l1Token,
                _l2Token,
                _to, // switched the _to and _from here to bounce back the deposit to the sender
                _from,
                _amount,
                _data
            );

            // Send message up to L1 bridge
            // slither-disable-next-line reentrancy-events
            sendCrossDomainMessage(l1TokenBridge, 0, message);
            // slither-disable-next-line reentrancy-events
            emit DepositFailed(_l1Token, _l2Token, _from, _to, _amount, _data);
        }
    }
```

## Impact

The missing handling of failed deposit transactions would lead to inconsistencies and incorrect token balances within the bridge mechanism. And could result in a loss of user funds or discrepancies between the L1 and L2 chains.

## Code Snippet

[finalizeDeposit()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/b440f89234b806f672b9e9ad24cf70c409964db5/op-eco/contracts/bridge/L2ECOBridge.sol#L145-L164)

Also see optimism's [L2StandardBridge.finaldeposit()](https://github.com/ethereum-optimism/optimism/blob/9ddfe5706f073eb0a1906216694e9b5e22ad4a8c/packages/contracts/contracts/L2/messaging/L2StandardBridge.sol#L141-L187)

## Tool used

Manual Review

## Recommendation

Fully implement the inherited function, also I recommend including proper error handling and revert the state changes in the `finalizeDeposit()` function when a deposit transaction fails. This will help maintain the consistency and integrity of the bridge mechanism by ensuring that the state of both L1 and L2 is correctly updated.
