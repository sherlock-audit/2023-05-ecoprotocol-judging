0xfuje

medium

# Too little `l2Gas` set on `depositERC20()` function could lead to lost deposited user funds

## Summary
Too little `l2Gas` set on `depositERC20()` function could lead to lost deposited user funds 

## Vulnerability Detail
The [depositERC20()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L190) function in [L1ECOBridge.sol](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol) deposits ECO into the contract and for the deposit to be finalized [finalizeDeposit()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L2ECOBridge.sol#L145) has to be called by the OP's L2CrossDomainMessenger contract that mints L2 Eco to the recipient. 

If you input less than 100_000 `l2Gas` to [depositERC20()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L190) OP's [CanonicalTransactionChain](https://github.com/ethereum-optimism/optimism/blob/develop/packages/contracts/contracts/L1/rollup/CanonicalTransactionChain.sol) [(0x5e4e65926ba27467555eb562121fac00d24e9dd2)](https://etherscan.io/address/0x5e4e65926ba27467555eb562121fac00d24e9dd2#code) contract that handles cross-chain calls reverts with "Transaction gas limit too low to enqueue." saving the user from a failing transaction on L2.

However in a likely scenario where user provides more gas than the transaction gas limit but less than what is necessary to execute `finalizeDeposit()` on L2 user funds will be lost in [L1ECOBridge.sol](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol) since [finalizeDeposit()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L2ECOBridge.sol#L145) can't execute and mint L2 ECO to the recipient.

Note: It's worth to note user funds could be recovered by a proxy update, however I still believe medium severity is justified because of the additional costs and complications of the potential recovery.

## Impact
User loses his deposited funds

## Code Snippet
[L1ECOBridge.sol - l2Gas in depositERC20()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L194)
[L1ECOBridge.sol - sendCrossDomainMessage called with l2Gas in _initiateERC20Deposit()](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L350)

## Proof of Concept

I initialized a foundry repository for mainnet fork testing on Optimism and Ethereum to test vulnerabilities. You have to have foundry installed but the test setup is relatively simple:

1. Copy Eco Foundry Fork test repo and install dependencies:
```bash
git clone https://github.com/0xfuje/eco-audit-foundry-setup.git && cd eco-audit-foundry-setup && forge install
```

2. Change .env Mainnet fork variables to your Alchemy or Infura ID or (other RPC provider ID):
```bash
MAINNET_RPC_URL="https://eth-mainnet.g.alchemy.com/v2/YOUR-ALCHEMY-MAINNET-ID"
OPTIMISM_RPC_URL="https://opt-mainnet.g.alchemy.com/v2/YOUR-ALCHEMY-OPTIMISM-ID"
```

3. Mainnet test won't work because another vulnerability (onlyFromCrossDomain Denial of Service on mainnet). In order for the test to work comment out the second require statement from the `onlyFromCrossDomainAccount` modifier from `src/bridge/CrossDomainEnabledUpgradeable.sol` which mitigates the mentioned vulnerability. Please make sure to remove the comment after the test is finished in order for other tests to work.

```solidity
modifier onlyFromCrossDomainAccount(address _sourceDomainAccount) {
    require(
        msg.sender == address(getCrossDomainMessenger()),
        "OVM_XCHAIN: messenger contract unauthenticated"
    );

    require(
        getCrossDomainMessenger().xDomainMessageSender() ==
            _sourceDomainAccount,
        "OVM_XCHAIN: wrong sender of cross-domain message"
    );
    _;
}
```

3. Copy the following code into test/EcoTestSetup.t.sol:
```solidity
contract TestL2GasFail is EcoTestSetup {
    function testL2GasFail() external {
        // select ethereum mainnet fork
        vm.selectFork(mainnetFork);
        uint256 depositAmount = 10e18;

        uint32 gasReverts = 99_999;
        uint32 gasPasses = 100_000;

        vm.startPrank(alice);
        eco.approve(address(l1EcoBridge), depositAmount);

        // depositERC20 call reverts with any gas value below 100_000
        vm.expectRevert();
        l1EcoBridge.depositERC20(
            address(eco),
            address(l2Eco),
            depositAmount,
            gasReverts,
            NON_NULL_BYTES32
        );
        
        // but you can accidentally deposit just enough that you can execute depositERC20
        // but not enough to execute the finalizeDeposit call on L2
        l1EcoBridge.depositERC20(
            address(eco),
            address(l2Eco),
            depositAmount,
            gasPasses,
            NON_NULL_BYTES32
        );
        vm.stopPrank();

        // switch to optimism mainnet fork
        vm.selectFork(optimismFork);
        // set optimism gas price to realistic value
        uint256 gasPrice = 3;
        vm.txGasPrice(gasPrice);

        // simulate op cross domain messenger call to finalize deposit
        vm.prank(L2_OP_CROSS_DOMAIN_MESSENGER);
        // measure gas before and after to calculate total gas used in the finalizeDeposit call
        uint256 gasStart = gasleft();
        l2EcoBridge.finalizeDeposit(
            address(eco),
            address(l2Eco),
            alice,
            alice,
            depositAmount * INITIAL_INFLATION_MULTIPLIER,
            NON_NULL_BYTES32
        );
        uint256 gasEnd = gasleft();
        uint256 gasUsed = (gasStart - gasEnd) * gasPrice;

        emit log_uint(gasPasses);
        emit log_uint(gasUsed);
        // gas is not enough for execution, gas used is more than provided
        // this means finalizeDeposit wouldn't execute on mainnet and user loses his funds
        assertLt(gasPasses, gasUsed);
    }
}
```
4. Run the test 
```bash
forge test --match-test testL2GasFail -vvvv
```

## Tool used
Manual Review, Foundry, Mainnet Fork Tests

## Recommendation
Set a minimum `l2Gas` limit that makes sure that `finalizeDeposit()` is executed on L2 in normal circumstances. Alternatively the front-end can warn the user about the minimum recommended L2 Gas limit.