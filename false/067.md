Dug

high

# Implementing `IL2StandardERC20` incorrectly signals that the pre-bedrock standard bridge can be used

## Summary

Because the `IL2StandardERC20` interface is implemented by `L2ECO`, user deposits into the pre-bedrock standard bridge, which would normally be reversed, will not be handled properly and funds will be lost.

## Vulnerability Detail

`IL2StandardERC20` was used by pre-bedrock Optimism contracts to signal that they were compatible with the standard bridge. There was an ERC165 check for it when deposits we're finalized. See the code below.

https://github.com/ethereum-optimism/optimism/blob/95978376a208e24a3ad03ba816dca89ae95a08dd/packages/contracts/contracts/L2/messaging/L2StandardBridge.sol#L141-L187
```solidity
    null(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _data
    ) external virtual onlyFromCrossDomainAccount(l1TokenBridge) {
        // Check the target token is compliant and
        // verify the deposited token on L1 matches the L2 deposited token representation here
        if (
            // slither-disable-next-line reentrancy-events
            ERC165Checker.supportsInterface(_l2Token, 0x1d1d8b63) &&
            _l1Token == IL2StandardERC20(_l2Token).l1Token()
        ) {
            // When a deposit is finalized, we credit the account on L2 with the same amount of
            // tokens.
            // slither-disable-next-line reentrancy-events
            IL2StandardERC20(_l2Token).mint(_to, _amount);
            // slither-disable-next-line reentrancy-events
            emit DepositFinalized(_l1Token, _l2Token, _from, _to, _amount, _data);
        } else {
            // Either the L2 token which is being deposited-into disagrees about the correct address
            // of its L1 token, or does not support the correct interface.
            // This should only happen if there is a  malicious L2 token, or if a user somehow
            // specified the wrong L2 token address to deposit into.
            // In either case, we stop the process here and construct a withdrawal
            // message so that users can get their funds out in some cases.
            // There is no way to prevent malicious token contracts altogether, but this does limit
            // user error and mitigate some forms of malicious contract behavior.
            bytes memory message = abi.encodeWithSelector(
                IL1ERC20Bridge.finalizeERC20Withdrawal.selector,
                _l1Token,
                _l2Token,
                _to, // switched the _to and _from here to bounce back the deposit to the sender
                _from,
                _amount,
                _data
            );

            // Send message up to L1 bridge
            // slither-disable-next-line reentrancy-events
            sendCrossDomainMessage(l1TokenBridge, 0, message);
            // slither-disable-next-line reentrancy-events
            emit DepositFailed(_l1Token, _l2Token, _from, _to, _amount, _data);
        }
    }
```

> Side note, you'll find that the `IL2StandardERC20` interface isn't used at all in Bedrock. Instead, an `IOptimismMintableERC20` interface is used to signal standard bridge compatibility. 

By implementing `IL2StandardERC20`, you signal that the token is compatible with the pre-bedrock standard bridge when actually, they are very incompatible.

An issue is created where users can deposit using the pre-bedrock standard bridge, however, when an attempt is made to finalize the deposit on L2, the transaction will revert as the standard bridge is unable to mint the L2 tokens because it does not have permission.

The fact that the ERC165 check passes, causes their transaction to not be reversed on L1 (the else block is not executed). The bridge would normally reverse the L1 deposit when `IL2StandardERC20` is not supported. 

## Impact

By signaling that L2ECO is supports the `IL2StandardERC20`, you cause the pre-bedrock standard bridge to permanently lock up user deposits.

## Code Snippet

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/token/L2ECO.sol#L264-L274

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/token/L2ECO.sol#L96-L99

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/token/L2ECO.sol#L229-L232

## Tool used

Manual Review

## Recommendation

If you do not support the pre-bedrock standard bridge, you should not implement the `IL2StandardERC20` interface.

```diff
    function supportsInterface(bytes4 _interfaceId) external pure returns (bool) {
-       bytes4 firstSupportedInterface = type(IERC165).interfaceId; // ERC165
-       bytes4 secondSupportedInterface = type(IL2StandardERC20).interfaceId; // compliant to OP's IL2StandardERC20
-       return _interfaceId == firstSupportedInterface || _interfaceId == secondSupportedInterface;
+       return _interfaceId == type(IERC165).interfaceId;
    }
```

By removing `IL2StandardERC20` as a supported interface, user deposits will correctly be returned to them on L1 if they attempt to deposit using the pre-bedrock standard bridge.