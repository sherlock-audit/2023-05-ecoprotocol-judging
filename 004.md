BugHunter101

high

# The inflationMultiplier parameter can be maliciously manipulated by attackers calling rebase() function

## Summary

The inflationMultiplier parameter can be maliciously manipulated by attackers calling rebase() function. When layer 1 and layer 2 communicate, it may cause user to loss funds.

## Vulnerability Detail

In L1ECOBridge.sol, the rebase() function:
```solidity
function rebase(uint32 _l2Gas) external {
        inflationMultiplier = IECO(l1Eco).getPastLinearInflation(
            block.number
        );

        bytes memory message = abi.encodeWithSelector(
            IL2ECOBridge.rebase.selector,
            inflationMultiplier
        );

        sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);
    }
```
When layer 1 and layer 2 communicate, it may cause user to loss funds..

Such as Alice call withdraw() in L2 with L2ECOBridge.sol
```solidity
function withdraw(
        address _l2Token,
        uint256 _amount,
        uint32 _l1Gas,
        bytes calldata _data
    ) external virtual isL2EcoToken(_l2Token) {
        _initiateWithdrawal(msg.sender, msg.sender, _amount, _l1Gas, _data);
    }
```
the _initiateWithdrawal() function will call `IL1ERC20Bridge.finalizeERC20Withdrawal` with `sendCrossDomainMessage`:
```solidity
function _initiateWithdrawal(
        address _from,
        address _to,
        uint256 _amount,
        uint32 _l1Gas,
        bytes calldata _data
    ) internal {
        // Burn the withdrawn tokens from L2
        l2Eco.burn(msg.sender, _amount);

        // Construct calldata for l1TokenBridge.finalizeERC20Withdrawal(_to, _amount)
        _amount = _amount * inflationMultiplier;
        bytes memory message = abi.encodeWithSelector(
            //call parent interface of IL1ECOBridge to get the selector
            IL1ERC20Bridge.finalizeERC20Withdrawal.selector,
            l1Eco,
            l2Eco,
            _from,
            _to,
            _amount,
            _data
        );

        // Send message up to L1 bridge
        sendCrossDomainMessage(l1TokenBridge, _l1Gas, message);

        // Emit event to notify L2 of withdrawal
        emit WithdrawalInitiated(
            l1Eco,
            address(l2Eco),
            msg.sender,
            _to,
            _amount,
            _data
        );
    }
```
And at this time, parameter inflationMultiplier in L2ECOBridge.sol has not changed. This function  `sendCrossDomainMessage` will put the message in the queue for processing. Meanwhile, Attacker Bob sees the message in the queue, he preemptively calls the function rebase(), so the global parameter `inflationMultiplier` in L1ECOBridge has changed. When Alice's message is passed here and executed   `finalizeERC20Withdrawal()` function, this results in a loss of Alice's money due to a change in parameter `inflationMultiplier`:
```solidity
function finalizeERC20Withdrawal(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _gonsAmount,
        bytes calldata _data
    ) external onlyFromCrossDomainAccount(l2TokenBridge) {
        uint256 _amount = _gonsAmount / inflationMultiplier; //@audit inflationMultiplier has changed

        // equivalent to IECO(_l1Token).transfer(_to, _amount); but is revert safe
        bytes memory _ecoTransferMessage = abi.encodeWithSelector(
            IERC20.transfer.selector,
            _to,
            _amount
        );
        (bool success, bytes memory returnData) = _l1Token.call{value: 0}(
            _ecoTransferMessage
        );

        // make sure that the call to transfer didn't revert or return false
        if (success && abi.decode(returnData, (bool))) {
            // if successful, emit an event
            emit ERC20WithdrawalFinalized(
                _l1Token,
                _l2Token,
                _from,
                _to,
                _amount,
                _data
            );
        } else {
            // if the transfer fails, create a return tx
            bytes memory message = abi.encodeWithSelector(
                IL2ERC20Bridge.finalizeDeposit.selector,
                _l1Token,
                _l2Token,
                _to, // switched the _to and _from here to bounce back the deposit to the sender
                _from,
                _gonsAmount,
                _data
            );

            // Send message up to L1 bridge
            sendCrossDomainMessage(l2TokenBridge, 0, message);
            // Emit an event to signal success event listeners to expect failure
            emit WithdrawalFailed(
                _l1Token,
                _l2Token,
                _from,
                _to,
                _amount,
                _data
            );
        }
    }
```


## Impact

It may cause user to loss funds.

## Code Snippet

https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L296

## Tool used

Manual Review

## Recommendation

Maybe the developers can add auth to the rebase() fcuntion
