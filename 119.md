aidenpearce369

false

# Unchecked return type validation in `_initiateERC20Deposit`

#### Name - AidenPearce369#8497

#### Severity - HIGH

## Summary
Due to the unchecked return value from `transferFrom()` leads to minting tokens on `L2 Bridge` even after the transaction fails from the `L1 Bridge` based on the `ERC20 Token` being used for the transaction.

## Vulnerability Detail

The `_initiateERC20Deposit()` function from `L1ECOBridge.sol` initiates transferring of tokens from owner's address to spender's address using `transferFrom()` function. Since we are handling transactions within a bridge, the `ERC20 Token` format may vary depending the protocol. When specific tokens like `DAI` are sent using  `_initiateERC20Deposit()`, the function 
`transferFrom()` should return a `bool` variable representing the state of the transaction. 

```js
// From IERC20
function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
```

## PoC

Consider the execution logic from the below snippet,

```js
        // @audit - Should check the return type for the transaction
        IECO(_l1Token).transferFrom(_from, address(this), _amount);
        // gons move across the bridge, with inflation multipliers on either side to correctly scale balances
       // @audit - Should process the below logic, if transaction gets succeeded and validated with a return value
        _amount = _amount * inflationMultiplier;

        // Construct calldata for _l2Token.finalizeDeposit(_to, _amount)
        bytes memory message = abi.encodeWithSelector(
            //call parent interface IL2ERC20Bridge to get the selector
            IL2ERC20Bridge.finalizeDeposit.selector,
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );

        // Send calldata into L2
        sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);

        emit ERC20DepositInitiated(
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );
 ```

Assume the transaction fails for `IECO(_l1Token).transferFrom(_from, address(this), _amount)`. Since `transferFrom()` returns `bool`, there is no check for validation the transaction state. Even though the transaction gets failed, the `_initiateERC20Deposit()` will start proceeding minting tokens on `L2 Bridge`.

## Impact

Due to the unchecked return type, an attacker can take leverage of the failed transaction from `transferFrom(_from, address(this), _amount)` to mint the same amount of ERC20 tokens into the `L2 Bridge` for his address. This may lead to improper validation and tracking of `ERC20 Tokens` across the two bridges and minting of money without proper asset transfer. 

## Code Snippet
[https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L321](https://github.com/sherlock-audit/2023-05-ecoprotocol/blob/main/op-eco/contracts/bridge/L1ECOBridge.sol#L321)

```js
function _initiateERC20Deposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _l2Gas,
        bytes calldata _data
    ) internal {
        // When a deposit is initiated on L1, the L1 Bridge transfers the funds to itself for future
        // withdrawals.

        // @note - since its msg.sender its not null
        // @audit - Transfers from function does not check the return value, whether the transaction is valid/failed
        IECO(_l1Token).transferFrom(_from, address(this), _amount);
        // gons move across the bridge, with inflation multipliers on either side to correctly scale balances
        _amount = _amount * inflationMultiplier;

        // Construct calldata for _l2Token.finalizeDeposit(_to, _amount)
        bytes memory message = abi.encodeWithSelector(
            //call parent interface IL2ERC20Bridge to get the selector
            IL2ERC20Bridge.finalizeDeposit.selector,
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );

        // Send calldata into L2
        sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);

        emit ERC20DepositInitiated(
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );
    }
```

## Tool used

Manual Review

## Recommendation

Implement a return value check for `transferFrom()` and perform the operations which performs changing state variables and other transactions.

```js
     function _initiateERC20Deposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _l2Gas,
        bytes calldata _data
    ) internal {
        // When a deposit is initiated on L1, the L1 Bridge transfers the funds to itself for future
        // withdrawals.
        bool checkSuccess = IECO(_l1Token).transferFrom(_from, address(this), _amount);
        require(checkSuccess, "Transaction Failed");
        // gons move across the bridge, with inflation multipliers on either side to correctly scale balances
        _amount = _amount * inflationMultiplier;

        // Construct calldata for _l2Token.finalizeDeposit(_to, _amount)
        bytes memory message = abi.encodeWithSelector(
            //call parent interface IL2ERC20Bridge to get the selector
            IL2ERC20Bridge.finalizeDeposit.selector,
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );

        // Send calldata into L2
        sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);

        emit ERC20DepositInitiated(
            _l1Token,
            _l2Token,
            _from,
            _to,
            _amount,
            _data
        );
    }
 ```
